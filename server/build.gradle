plugins {
    id 'application'
    id 'jacoco'

    // https://github.com/gmazzo/gradle-buildconfig-plugin
    id "com.github.gmazzo.buildconfig" version "3.0.0"
}

group 'bg.sofia.uni.fmi.mjt.password.vault'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13'

    implementation 'com.google.code.gson:gson:2.8.9'

    implementation 'org.mockito:mockito-core:3.7.7'

    implementation 'org.mariadb.jdbc:mariadb-java-client:2.1.2'

    implementation project(':api')
}

test {
    useJUnit()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

jacoco {
    toolVersion = "0.8.7"
}

buildConfig {
    className("MyConfig")   // forces the class name. Defaults to 'BuildConfig'
    packageName("password.vault.server")  // forces the package. Defaults to '${project.group}'

    useJavaOutput() // forces the outputType to 'java'

    def HAPPY_DEV_AUTH_KEY = property('HAPPY_DEV_AUTH_KEY')
    def ENZOIC_AUTH_KEY = property('ENZOIC_AUTH_KEY')

    def DB_URL = findProperty('DB_URL')
    def DB_USER = findProperty('DB_USER')
    def DB_PASSWORD = findProperty('DB_PASSWORD')

    buildConfigField('String', 'HAPPY_DEV_AUTH_KEY', "\"${HAPPY_DEV_AUTH_KEY}\"")
    buildConfigField('String', 'ENZOIC_AUTH_KEY', "\"${ENZOIC_AUTH_KEY}\"")

    buildConfigField('String', 'DB_URL', "\"${DB_URL}\"")
    buildConfigField('String', 'DB_USER', "\"${DB_USER}\"")
    buildConfigField('String', 'DB_PASSWORD', "\"${DB_PASSWORD}\"")
}

// add this to read from console
run {
    standardInput = System.in
}

application {
    mainClass = 'password.vault.server.Main'
}
